buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.7'
}

apply plugin: 'net.minecraftforge.gradle.forge'

configurations {
    shade
    compile.extendsFrom shade
}

version = "1.8.9-0.7.6"
group = "totemic_commons.pokefenn"
archivesBaseName = "Totemic"

minecraft {
    version = "1.8.9-11.15.1.1902-1.8.9"
    runDir = "run"

    mappings = "stable_20"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    replaceIn "Totemic.java"
    replace "\${version}", project.version
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    maven {
        name "tterrag Repo"
        url "http://maven.tterrag.com/"
    }
}

dependencies {
    shade "chylex.javacheck:JavaChecker:v2.0-b3"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Shade Java 8 checker
reobf {
    jar {
        extraLines "PK: chylex/javacheck totemic_commons/shade/javacheck"
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }

    manifest {
        attributes 'TweakClass': 'totemic_commons.shade.javacheck.Java8Checker'
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        appendix = 'deobf'
    }
}

// Create API library zip
task apiZip(type: Zip) {
    from(sourceSets.main.java) {
        include 'vazkii/**'
        include 'totemic_commons/pokefenn/api/**'
    }
    classifier = 'api'
}

apiZip.mustRunAfter deobfJar

artifacts {
    archives deobfJar
    archives apiZip
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

    project {
        id = '237541'
        changelog  = ''
        releaseType = 'beta'

        addArtifact deobfJar
        addArtifact apiZip
    }
}
