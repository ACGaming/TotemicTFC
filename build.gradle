buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.12.2-0.11.7"
group = "pokefenn.totemic"
archivesBaseName = "TotemicTFC"

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20180529"
    replaceIn "Totemic.java"
    replace "\${version}", project.version
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

jar.manifest {
    attributes 'FMLAT': 'totemic_at.cfg'
}

sourceCompatibility = targetCompatibility = '1.8'

repositories {
    maven {
        url "http://maven.tehnut.info"
    }
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        appendix = 'deobf'
    }
}

task apiSrcJar(type: Jar) {
    from(sourceSets.main.java) {
        include 'pokefenn/totemic/api/**'
        appendix = 'api'
    }
    classifier = 'sources'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

reobfJar.finalizedBy signJar

artifacts {
    archives deobfJar
    archives apiSrcJar
}